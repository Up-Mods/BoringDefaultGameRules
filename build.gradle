plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
	alias libs.plugins.shadow
}

base.archivesName = project.archives_base_name
version = "$project.version+${libs.versions.minecraft.get()}"
group = project.maven_group

repositories {
	maven { url 'https://maven.terraformersmc.com/releases/' }
}

loom {
	mods {
		"boring_default_game_rules" {
			sourceSet('main')
		}
	}

	mixin {
		useLegacyMixinAp = false
	}

	accessWidenerPath = file("src/main/resources/boring_default_game_rules.accesswidener")
}

// All the dependencies are declared at gradle/libs.version.toml and referenced with "libs.<id>"
// See https://docs.gradle.org/current/userguide/platforms.html for information on how version catalogs work.
dependencies {
	minecraft libs.minecraft
	mappings variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' }
	modImplementation libs.quilt.loader

	modImplementation libs.quilted.fabric.api
	modImplementation(libs.mod.menu) {
		transitive = false
	}

	implementation libs.quilt.parsers.json
	shadow(libs.quilt.parsers.json) {
		transitive = false
	}
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand 'version': version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

remapJar {
	archiveClassifier = 'shadowless'
}

tasks.register('shadowRemapJar', ShadowJar).configure {
	dependsOn remapJar
	from remapJar.outputs

	from('LICENSE') {
		rename { "${it}_${base.archivesName.get()}" }
	}

	archiveClassifier = null
	configurations = [ project.configurations.shadow ]

	relocate 'org.quiltmc.parsers', 'io.github.ennuil.quilt_parsers'
}

tasks.named('build').configure {
	dependsOn tasks.named('shadowRemapJar')
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
